define 

######
### Molecular Schema
######

variant sub entity,
	owns snpId,
	plays chromosome-location:located-variant;
chromosome sub entity, 
	owns chromosome-name,
	plays chromosome-location:locating-chromosome;
gene sub entity,
    owns gene-symbol,
    owns entrez-id,
    owns ensembl-gene-stable-id,
    owns ensembl-xref,
    owns gene-name,
    plays gene-protein-encoding:encoding-gene,
    plays transcription:transcribing-gene,
    plays gene-disease-association:associated-gene,
    plays gene-tissue-association:associated-gene,
    plays drug-gene-interaction:target-gene,
	plays gene-virus-association:associated-virus-gene,
	plays chromosome-location:located-gene;
transcript sub entity, 
	owns ensembl-transcript-stable-id, 
	plays transcription:encoded-transcript,
	plays translation:translating-transcript;
protein sub entity,
    owns uniprot-id,
    owns uniprot-entry-name,
    owns uniprot-symbol,
    owns uniprot-name,
    owns ensembl-protein-stable-id, 
    owns go-evidence-code,
    owns go-taxon,
    owns go-id,
    owns function-description,
    plays modulation:modulated,
	plays protein-protein-interaction:activating-protein,
	plays protein-protein-interaction:activated-protein,
    plays translation:translated-protein,
    plays protein-complex:associated-protein, 
    plays gene-protein-encoding:encoded-protein,
    plays tissue-enhancement:expressed-protein,
    plays protein-protein-interaction:interacting-protein,
    plays pathway-participation:participating-protein,
    plays drug-protein-interaction:target-protein,
    plays organism-association:associating,
    plays protein-virus-association:hosting-virus-protein;
disease sub entity,
    owns disease-name,
    owns disease-id,
    owns disease-type,
	plays disease-class-association:categorised-disease,
    plays protein-disease-association:associated-disease,
    plays drug-disease-association:affected-disease;
disease-class sub entity, 
	owns class-name,
	plays disease-class-association:associated-class;
drug sub entity,
    owns drug-name,
    owns chembl-id,
    owns drug-claim-name,
    owns drug-claim-source,
    plays drug-protein-interaction:interacting-drug,
    plays drug-protein-interaction:inhibitor,
    plays drug-protein-interaction:substrate,
    plays drug-protein-interaction:antagonist,
    plays drug-protein-interaction:blocker,
    plays drug-disease-association:therapeutic,
    plays drug-gene-interaction:interacting-drug,
    plays drug-tissue-association:associated-drug,
    plays drug-compound-combination:resulting-drug;
compound sub entity,
	plays drug-compound-combination:participating-compound,
	plays modulation:modulator;
tissue sub entity,
    owns tissue-name,
    plays tissue-enhancement:enhanced-tissue,
    plays process-localisation:tissue-context,
    plays gene-tissue-association:associated-tissue;
pathway sub entity,
    owns pathway-name,
    owns pathway-id,
    plays pathway-participation:participated-pathway;
organism sub entity,
	owns organism-name,
	plays organism-association:associated-organism,
	plays organism-virus-hosting:hosting-organism;
virus sub entity, 
	owns genbank-id, 
	owns virus-name, 
	owns identity-percentage,
	plays organism-virus-hosting:hosted-virus,
	plays gene-virus-association:associated-virus,
	plays discovery:discovered-virus;

## Relations
transcription sub relation, 
	relates transcribing-gene,
	relates encoded-transcript;
translation sub relation, 
	relates translated-protein,
	relates translating-transcript;
protein-complex sub relation, 
	owns complexportal-name,
	owns complexportal-id,
	owns complexportal-object-symbol,
	owns go-id,
	owns go-taxon,
	owns pmid,
	owns go-evidence-code,
	relates associated-protein;
gene-protein-encoding sub relation,
	relates encoded-protein,
	relates encoding-gene;
protein-disease-association sub relation,
    relates associated-protein,
    relates associated-disease;
gene-disease-association sub relation,
	owns disgenet-score,
    relates associated-gene,
    relates associated-disease;
gene-tissue-association sub relation,
	relates associated-tissue,
	relates associated-gene;
drug-protein-interaction sub relation,
    relates inhibitor,
    relates substrate,
    relates antagonist,
    relates blocker,
    relates interacting-drug,
    relates target-protein;
drug-gene-interaction sub relation,
	owns interaction-type,
    relates target-gene,
    relates interacting-drug;
tissue-enhancement sub relation,
    relates enhanced-tissue,
    relates expressed-protein;
process-localisation sub relation,
    relates biomolecular-process,
    relates tissue-context;
protein-protein-interaction sub relation,
    relates interacting-protein,
    relates activating-protein,
    relates activated-protein,
    plays process-localisation:biomolecular-process;
drug-disease-association sub relation,
    relates affected-disease,
    relates therapeutic;
pathway-participation sub relation,
	relates participating-protein,
	relates participated-pathway;
disease-tissue-association sub relation,
    relates associated-disease,
    relates associated-tissue;
drug-tissue-association sub relation,
    relates associated-drug,
    relates associated-tissue;
drug-compound-combination sub relation, 
	relates participating-compound,
	relates resulting-drug;
modulation sub relation,
	relates modulator,
	relates modulated;
organism-association sub relation, 
	relates associated-organism,
	relates associating;
organism-virus-hosting sub relation,
	relates hosted-virus,
	relates hosting-organism;
protein-virus-association sub relation,
	relates associated-virus,
	relates hosting-virus-protein;
gene-virus-association sub relation, 
	relates associated-virus,
	relates associated-virus-gene;
discovery sub relation,
	relates discovered-virus,
	relates discovering-location;
chromosome-location sub relation,
	owns start-location, 
	owns end-location,
	relates located-variant,
	relates located-gene,
	relates locating-chromosome;
disease-class-association sub relation,
	relates categorised-disease,
	relates associated-class; 


## Attributes
# Name attributes: 
name sub attribute, value string, abstract;
	chromosome-name sub name;
	class-name sub name; 
	disease-name sub name;
	disease-type sub name;
	database-name sub name;
	uniprot-name sub name;
	gene-symbol sub name;
	ensembl-xref sub name; 
	uniprot-symbol sub name; 
	complexportal-name sub name;
	complexportal-object-symbol sub name;
	gene-name sub name;
	protein-name sub name;
	uniprot-entry-name sub name;
	pathway-name sub name;
	tissue-name sub name;
	drug-name sub name;
	organism-name sub name;
	virus-name sub name; 
	drug-claim-name sub name;

pmid sub attribute, value string; 


identifier sub attribute, value string, abstract;
	complex-id sub identifier, abstract;
		complexportal-id sub complex-id;
	gene-id sub identifier, abstract;
		ensembl-gene-stable-id sub gene-id;
		entrez-id sub gene-id;
	protein-id sub identifier, abstract;
		uniprot-id sub protein-id;
		ensembl-protein-stable-id sub protein-id;
		uniprot-id sub protein-id;
		ensembl-id sub protein-id;
	transcript-id sub identifier, abstract;
		ensembl-transcript-stable-id sub transcript-id;
	go-id sub identifier;
	disease-id sub identifier;
	genbank-id sub identifier;

	GEOStudy-id sub identifier;
	GEOComparison-id sub identifier;
	pathway-id sub identifier;
	chembl-id sub identifier;
	snpId sub identifier;


  
drug-claim-source sub attribute, value string;
identity-percentage sub attribute, value string;
go-evidence-code sub attribute, value string; 
go-taxon sub attribute, value long; 
function-description sub attribute, value string;
start-location sub attribute, value long;
end-location sub attribute, value long;
disgenet-score sub attribute, value double;
interaction-type sub attribute, value string;